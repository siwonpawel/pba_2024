plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'

    id("org.openapi.generator") version "7.10.0"
}

group = 'com.github.siwonpawel'
version = '0.0.1-SNAPSHOT'

openApiGenerate {
    generatorName = 'spring'

    inputSpec = "$projectDir/src/main/resources/users.yaml"
    outputDir = "$buildDir/generated/sources/openapi"

    apiPackage = 'pl.edu.zut.pba.users.api'
    modelPackage = 'pl.edu.zut.pba.users.api.model'

    generateApiTests = false
    generateModelTests = false;

    configOptions.set([
            useSpringBoot3       : 'true',
            interfaceOnly        : 'true',
            performBeanValidation: 'true',
            skipDefaultInterface : 'true',
            useTags              : 'true'
    ])

    globalProperties.set([
            models: '',
            apis  : ''
    ])
}

tasks.compileJava.dependsOn(tasks.openApiGenerate)


sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/sources/openapi/src/main/java"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    runtimeOnly 'com.h2database:h2'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

    implementation 'org.hibernate.validator:hibernate-validator'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
}
